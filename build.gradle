buildscript {
    ext.kotlin_version = '1.2.30'
    ext.dokkaVersion = '0.9.15'

    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.13.1"
    id "me.champeau.gradle.jmh" version "0.3.1"
}

group 'com.github.penemue'
version = hasProperty('keapVersion') ? project.keapVersion : '0.1.0-SNAPSHOT'
def isSnapshot = version.endsWith('SNAPSHOT')
def mavenPublishUrl = hasProperty('mavenPublishUrl') ? project.mavenPublishUrl : ''
def mavenPublishUsername = hasProperty('mavenPublishUsername') ? project.mavenPublishUsername : ''
def mavenPublishPassword = hasProperty('mavenPublishPassword') ? project.mavenPublishPassword : ''
def signingKeyId = hasProperty('signingKeyId') ? project.signingKeyId : ''
def signingPassword = hasProperty('signingPassword') ? project.signingPassword : ''
def signingSecretKeyRingFile = hasProperty('signingSecretKeyRingFile') ? project.signingSecretKeyRingFile : ''

task wrapper(type: Wrapper) {
    gradleVersion = '3.5.1'
}

defaultTasks 'clean', 'build'

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven'
apply plugin: 'signing'

license {
    header rootProject.file('copyright.ftl')
    strictCheck true
    ext.inceptionYear = 2016
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.owner = 'Vyacheslav Lukianov'
    ext.ownerURL = 'https://github.com/penemue'
    include "**/*.kt"
    mapping {
        kt = 'JAVADOC_STYLE'
    }
}

repositories {
    jcenter()
}

test {
    // uncomment the following line in order to test mergeSort instead of timSort
    //jvmArgs = ['-Djava.util.Arrays.useLegacyMergeSort=true']
    testLogging.showStandardStreams = true
}

jmh {
    jmhVersion = '1.19'
    jvmArgsPrepend '-Xmx1g -Xms1g'
    duplicateClassesStrategy = 'warn'
    forceGC = true
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions {
        languageVersion = '1.2'
        apiVersion = '1.2'
    }
}
compileTestKotlin {
    kotlinOptions {
        languageVersion = '1.2'
        apiVersion = '1.2'
    }
}
dokka {
    jdkVersion = 7
    packageOptions {
        reportUndocumented = false
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': version
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from project.sourceSets.main.kotlin
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}

artifacts {
    archives jar, javadocJar, sourceJar
}

if (!isSnapshot) {
    ext.'signing.keyId' = signingKeyId
    ext.'signing.password' = signingPassword
    ext.'signing.secretKeyRingFile' = signingSecretKeyRingFile
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {

                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: mavenPublishUrl) {
                    authentication(userName: mavenPublishUsername, password: mavenPublishPassword)
                }

                pom.project {
                    name 'Keap'
                    description 'Keap is a heap data structure presenting stable PriorityQueue and stable Keapsort sorting algorithm'
                    packaging 'jar'
                    url 'https://github.com/penemue/keap'

                    scm {
                        url 'https://github.com/penemue/keap'
                        connection 'scm:git:https://github.com/penemue/keap.git'
                        developerConnection 'scm:git:https://github.com/penemue/keap.git'
                    }

                    inceptionYear '2016'
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'penemue'
                            name 'Vyacheslav Lukianov'
                        }
                    }
                }
            }
        }
    }
    signing {
        required { !isSnapshot && gradle.taskGraph.hasTask('uploadArchives') }
        sign configurations.archives
    }
}